if(NOT _ghex_already_fetched)
  find_package(GHEX QUIET)
endif()
if(NOT GHEX_FOUND)
    #set(_ghex_repository "https://github.com/GridTools/GHEX.git")
  set(_ghex_repository "https://github.com/boeschf/GHEX.git")
  set(_ghex_tag        "benchmarking")
  if(NOT _ghex_already_fetched)
    message(STATUS "Fetching GHEX ${_ghex_tag} from ${_ghex_repository}")
  endif()
  include(FetchContent)
  FetchContent_Declare(
    ghex
    GIT_REPOSITORY ${_ghex_repository}
    GIT_TAG        ${_ghex_tag}
  )
  FetchContent_MakeAvailable(ghex)
  set(_ghex_already_fetched ON CACHE INTERNAL "")
endif()
if(_cartex_cuda_enabled)
  set(USE_GPU ON CACHE BOOL "Use cuda in GHEX" FORCE)
  target_compile_definitions(runtime PRIVATE USE_GPU)
  mark_as_advanced(USE_GPU)
else()
  set(USE_GPU OFF CACHE BOOL "Use cuda in GHEX" FORCE)
  mark_as_advanced(USE_GPU)
endif()
mark_as_advanced(USE_HYBRID_TESTS)
mark_as_advanced(GHEX_BUILD_BENCHMARKS)
mark_as_advanced(GHEX_BUILD_FORTRAN)
mark_as_advanced(GHEX_BUILD_TESTS)
mark_as_advanced(GHEX_ENABLE_ATLAS_BINDINGS)
mark_as_advanced(GHEX_SKIP_MPICXX)

compile_as_cuda(runtime.cpp TARGET_DIRECTORY runtime)
target_sources(runtime PRIVATE runtime.cpp)
target_link_libraries(runtime PUBLIC GHEX::ghexlib)
target_compile_options(runtime PUBLIC "$<$<COMPILE_LANGUAGE:CUDA>:SHELL:--default-stream per-thread>")

if(GHEX_USE_UCP)
  target_compile_definitions(runtime PRIVATE GTBENCH_USE_GHEX_UCP)
  if(GHEX_USE_PMIX)
    target_compile_definitions(runtime PRIVATE GTBENCH_USE_GHEX_PMIX)
  endif()
endif()
if(GHEX_USE_XPMEM)
  target_compile_definitions(runtime PRIVATE GHEX_USE_XPMEM)
  if(GHEX_USE_XPMEM_ACCESS_GUARD)
    target_compile_definitions(runtime PRIVATE GHEX_USE_XPMEM_ACCESS_GUARD)
  endif()
endif()
if(GHEX_COMM_OBJ_FAT_CALLBACKS)
  target_compile_definitions(runtime PRIVATE GHEX_COMM_OBJ_FAT_CALLBACKS)
endif()
if(GHEX_COMM_OBJ_U_PACK)
  target_compile_definitions(runtime PRIVATE GHEX_COMM_OBJ_U_PACK)
endif()
